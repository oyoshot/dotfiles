# history
export HISTFILE="$XDG_STATE_HOME/zsh_history"
HISTSIZE=1000000
SAVEHIST=1000000
setopt extended_history # HISTFILE にコマンドの開始時刻と実行時間を一緒に保存する
setopt hist_ignore_all_dups # 同じコマンドをヒストリに残さない
setopt hist_ignore_space # スペースから始まるコマンド行はヒストリに残さない
setopt hist_reduce_blanks # ヒストリに保存するときに余分なスペースを削除する
setopt hist_save_no_dups # 履歴ファイルへ書き出すときに重複コマンドを保存しない
setopt inc_append_history # 実行直後に即ファイルへ追記
setopt share_history # 同時に起動したzshの間でヒストリを共有する

# options
setopt print_eight_bit # 日本語ファイル名を表示可能にする
setopt no_beep # beep を無効にする
setopt no_flow_control # フローコントロールを無効にする
setopt ignore_eof # Ctrl+Dでzshを終了しない
setopt interactive_comments # '#' 以降をコメントとして扱う
setopt auto_cd # ディレクトリ名だけでcdする
setopt auto_pushd # cd したら自動的にpushdする
setopt pushd_ignore_dups # 重複したディレクトリを追加しない
setopt extended_glob # 高機能なワイルドカード展開を使用する

bindkey '^[[1~' beginning-of-line # HOME
bindkey '^[[2~' overwrite-mode # INS
# bindkey '^[[2~' bracketed-paste  # シンプルにクリップボード貼り付けにしてしまうzsh 5.9 以降
bindkey '^[[3~' delete-char # DEL
bindkey '^[[4~' end-of-line # END
bindkey '^[[5~' history-beginning-search-backward # PageUp
bindkey '^[[6~' history-beginning-search-forward # PageDown

bindkey '^[[1;5A' up-line-or-history # Ctrl+Up
bindkey '^[[1;5B' down-line-or-history # Ctrl+Down
bindkey '^[[1;5C' forward-word # Ctrl+Right
bindkey '^[[1;5D' backward-word # Ctrl+Left

# fpath
fpath=(
  $ZDOTDIR/fpath(N-/)
  $ZDOTDIR/plugins/zsh-completions/src(N-/)
  $ZDOTDIR/plugins/anyframe(N-/)
  $ZDOTDIR/zfunc(N-/)
  ~/.asdf/completions(N-/)
  $GHRED_DATA_HOME/completions(N-/)
  $XDG_DATA_HOME/zsh/completions(N-/)
  $fpath
)

# defer loader
source $ZDOTDIR/plugins/zsh-defer/zsh-defer.plugin.zsh

# load configs
for f ($ZDOTDIR/sync/*(N-.)) source $f
for f ($ZDOTDIR/defer/*(N-.)) zsh-defer source $f

# plugins
zsh-defer source $ZDOTDIR/plugins/autosuggestions/zsh-autosuggestions.zsh
zsh-defer source $ZDOTDIR/plugins/syntax-highlighting/zsh-syntax-highlighting.zsh
zsh-defer source $ZDOTDIR/plugins/zsh-history-substring-search/zsh-history-substring-search.zsh
zsh-defer source $ZDOTDIR/fpath/aws_zsh_completer.sh
zsh-defer source $ZDOTDIR/defer.zsh
zsh-defer '[[ -r "$XDG_CONFIG_HOME/op/plugins.sh" ]] && source "$XDG_CONFIG_HOME/op/plugins.sh"'

# Mise
autoload -Uz add-zsh-hook
typeset -g __MISE_ACTIVATED=0

__mise_activate_once() {
  (( __MISE_ACTIVATED )) && return 0
  __MISE_ACTIVATED=1

  export MISE_JOBS=$(( $(getconf _NPROCESSORS_ONLN 2>/dev/null || echo 1) + 1 ))
  export MISE_NODE_DEFAULT_PACKAGES_FILE="$XDG_CONFIG_HOME/mise/default-npm-packages"
  (( $+functions[__load_github_token_once] )) && __load_github_token_once
  eval "$(mise activate zsh)"
  if (( $+functions[mise] )); then
    functions[_mise_internal]="$functions[mise]"
    mise() {
      (( $+functions[__load_github_token_once] )) && __load_github_token_once
      _mise_internal "$@"
    }
  fi
}

if (( $+commands[mise] )); then
  if [[ -o interactive ]]; then
    __mise_preexec_once() {
      __mise_activate_once
      add-zsh-hook -d preexec __mise_preexec_once 2>/dev/null || true
    }
    add-zsh-hook preexec __mise_preexec_once
  else
    __mise_activate_once
  fi
fi

# 遅延ロードしない方が安全
# Starship
(( ${+commands[starship]} )) && eval "$(starship init zsh)"

# load override
for cand in "$ZDOTDIR/zshrc.local" "$XDG_CONFIG_HOME/zsh/zshrc.local" "$HOME/zshrc.local"; do
  [[ -r $cand ]] && source "$cand" && break
done
